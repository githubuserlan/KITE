@printer KlickdummyText
Hallo, bevor es los geht, werden vorab ein paar Infos benötigt, damit der Dialogtext personalisiert werden kann. 
Die angegebenen Daten werden vertraulich behandelt und werden nur lokal auf deinem Gerät gespeichert.
Wenn du willst, kannst du auch die Standardwerte in den Feldern lassen.

@input PlayerFirstName summary:"Vorname" value:"Erika"
@stop
@input PlayerName summary:"Nachname" value:"Musterfrau"
@stop
@input PlayerBusinessName summary:"Der Name deines Unternehmens" value:"Unternehmensname"
@stop
@input PlayerBusinessIdea summary:"Deine Gründungsidee" value:"Gründungsidee"
@stop
@input PlayerBusinessGoal summary:"Ziel meines Unternehmens ist es:" value:"Unternehmensziel"
@stop
@set MaleChar="Herr Mayer"

@showUI Map
@bgm BGM_Map volume:0.5 loop:true
@hidePrinter
@stop

# startDialogue
@hideUI Map
@stopBgm BGM_Map
@showPrinter KlickdummyText
@back Intro_Klickdummy id:Intro_Klickdummy
Intro: In diesem Gebäude wirst Du mit deinem Ansprechpartner eine Unterhaltung über deine Geschäftsidee führen,
Intro: um hoffentlich Informationen zu passenden Förderungen zu erhalten und mit deinem Ideenpapier zu überzeugen.

@back Office_Klickdummy.Crossfade id:Office_Klickdummy

@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Guten Tag, Frau {PlayerName}. Haben Sie gut hergefunden?
@choice "Ja, es gab nur viel Verkehr." goto:.player_traffic
@choice "Ja, aber es gab kaum Parkplätze." goto:.player_parkingSpot
@choice "Ich habe das Gebäude zunächst nicht gefunden." goto:.player_proceed
@stop

;----------------------------------------------------------
# player_traffic
@char FemaleCharacter.Talking
@char MaleCharacter.Neutral
{PlayerFirstName}: Guten Tag. Ja, ich habe gut hergefunden.
{PlayerFirstName}: Es war nur sehr viel Verkehr. Es freut mich, heute hier zu sein.

@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Das freut mich zu hören. Zu dieser Uhrzeit sind immer viele Leute unterwegs.
@goto .mainDialogue

;----------------------------------------------------------
# player_parkingSpot
@char FemaleCharacter.Talking
@char MaleCharacter.Neutral
{PlayerFirstName}: Guten Tag. Ja, ich habe gut hergefunden.
{PlayerFirstName}: Es war nur schwer einen Parkplatz zu finden. Es freut mich, heute hier zu sein.  

@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Die Parksituation hier ist tatsächlich sehr schwierig. Die Angestellten beschweren sich auch ständig.
@goto .mainDialogue

;----------------------------------------------------------
# player_proceed
@char FemaleCharacter.Talking
@char MaleCharacter.Neutral
{PlayerFirstName}: Guten Tag. Ich habe das Gebäude zuerst nicht gesehen, 
{PlayerFirstName}: aber dann doch noch die richtige  Straße gefunden. Es freut mich, heute hier zu sein. 

@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Das kann jedem mal passieren. Die Straßenführung hier ist nicht die beste.
@goto .mainDialogue

;----------------------------------------------------------
# mainDialogue
{MaleChar}: Sie haben mir ja bereits Ihren vorläufigen Businessplan zugeschickt und er liest sich auch sehr gut.
{MaleChar}: Könnten Sie mir dennoch in zwei bis drei Sätzen Ihre Gründungsidee pitchen?
{PlayerFirstName}: Gerne. Also meine Idee ist {PlayerBusinessIdea}. Das Unternehmen soll den Namen {PlayerBusinessName} tragen. Ziel ist es {PlayerBusinessGoal}.

{MaleChar}: Das hört sich nach einer sehr innovativen Idee an.
{MaleChar}: Und Sie haben in Ihren Papieren gut herausgearbeitet, wie Sie damit einen Profit erzielen können.
{MaleChar}: Ich halte eine Förderung für gut möglich.
{MaleChar}: Allerdings ist es natürlich den Förderern wichtig, dass das Unternehmen über lange Zeit besteht und Geld einbringt.
{MaleChar}: Da es in der Vergangenheit schon vorkam, dass ein Unternehmen wegen Kindern aufgegeben werden musste,
{MaleChar}: würde es mich interessieren, wie es bei Ihnen mit dem Kinderwunsch beziehungsweise mit dem Familienstand steht.
@char MaleCharacter.Neutral

;------------Popup mit Erklärung einfügen
@showUI PopUp
@printer PopupPrinter
Analyse: Wie fühlst du dich bei dem Gespräch bis jetzt?
@choice "Verärgert" goto:.analysis_upset
@choice "Ermutigt" goto:.analysis_encouraged
@choice "Nervös" goto:.analysis_nervous
@choice "Ängstlich" goto:.analysis_scared
@stop

# analysis_encouraged
Analyse: Deine Idee scheint überzeugt zu haben, also ist ein ermutigtes Gefühl durchaus angemessen! Du hast schließlich die Fakten über deine Geschäftsidee im Kopf und weißt, was du damit erreichen willst.
@goto .mainDialogue2

# analysis_upset
Analyse: Auch heutzutage wird man manchmal bei Gesprächen zu einer Förderung immer noch mit Sexismus konfrontiert. Fragen wie diese hier beruhen teilweise auch unbewusst auf der Annahme, dass Frauen weniger leisten können, wenn sie eine Familie haben oder mit ihren Geschäftsideen weniger Erfolg haben als Männer. Tatsächlich ist es aber so, dass von Frauen geführte Start-ups und Unternehmen besser performen als die von Männern. 
@goto .mainDialogue2

# analysis_nervous
Analyse: In dieser Situation nervös zu sein ist verständlich - Du musst schließlich deine eigene Geschäftsidee erklären und weißt nicht, ob sie positiv aufgenommen wird.
@goto .mainDialogue2

# analysis_scared
Analyse: Dieses Gefühl ist vollkommen normal, ein Unternehmen zu gründen ist ein großer Schritt und die Geschäftsidee einer fremden Person vorzustellen ohne zu wissen, wie sie reagiert ist nervenaufreibend.
@goto .mainDialogue2

# mainDialogue2
@hideUI PopUp
@printer KlickdummyText
@char MaleCharacter.Talking
;------------Popup Ende

{MaleChar}: Da es in der Vergangenheit schon vorkam, dass ein Unternehmen wegen Kindern aufgegeben werden musste, würde es mich interessieren, wie es bei Ihnen mit dem Kinderwunsch bzw. mit dem Familienstand steht.
@choice "Ich habe bereits Kinder, aber es ist für mich kein Problem ein Unternehmen zu führen." goto:.children
@choice "Ich habe noch keine Kinder und dies ist für mich die nächste Zeit auch nicht geplant." goto:.noChildren
@choice "Meinen Kinderwunsch habe ich bereits bei der Unternehmensplanung mit bedacht." goto:.childDesire
@choice "Ich finde diese Frage ungerechtfertigt und unpassend." goto:.inappropriatedQuestion
@choice "Ich sehe keinen Grund, diese Frage beantworten zu müssen." goto:.noAnswer
@stop

# children
@char FemaleCharacter.Talking
@char MaleCharacter.Neutral
{PlayerFirstName}: Ich habe bereits Kinder, aber es ist für mich kein Problem ein Unternehmen zu führen. Ich kann mich auf die Unterstützung meiner Familie verlassen!
@set randomText=Random("neutral","positiv","rejecting")
@goto .reaction_neutral if:randomText="neutral"
@goto .reaction_positiv if:randomText="positiv"
@goto .reaction_neutral if:randomText="rejecting"

# noChildren
@char FemaleCharacter.Talking
@char MaleCharacter.Neutral
{PlayerFirstName}: Ich habe noch keine Kinder und dies ist für mich die nächste Zeit auch nicht geplant.
@set randomText=Random("neutral","positiv")
@goto .reaction_neutral if:randomText="neutral"
@goto .reaction_positiv if:randomText="positiv"

# childDesire
@char FemaleCharacter.Talking
@char MaleCharacter.Neutral
{PlayerFirstName}: Ich habe zwar einen Kinderwunsch, dies aber bereits bei der Unternehmensplanung mit bedacht. Es sollte mich also nicht behindern.
@set randomText=Random("neutral","positiv","rejecting")
@goto .reaction_neutral if:randomText="neutral"
@goto .reaction_positiv if:randomText="positiv"
@goto .reaction_neutral if:randomText="rejecting"

# inappropriatedQuestion
@char FemaleCharacter.Angry
@char MaleCharacter.Neutral
{PlayerFirstName}: Ich finde diese Frage ungerechtfertigt und unpassend. Hätten Sie sie auch einem Mann gestellt?
@set randomText=Random("sorry1","sorry2","explaining")
@goto .reaction_sorry1 if:randomText="sorry1"
@goto .reaction_sorry2 if:randomText="sorry2"
@goto .reaction_explaining if:randomText="explaining"

# noAnswer
@char FemaleCharacter.Angry
@char MaleCharacter.Neutral
{PlayerFirstName}: Ich sehe keinen Grund, diese Frage beantworten zu müssen.
@set randomText=Random("sorry1","sorry2")
@goto .reaction_sorry1 if:randomText="sorry1"
@goto .reaction_sorry2 if:randomText="sorry2"

;------------------------------Reactions
# reaction_neutral
@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Ich verstehe.
@goto .endDialogue

# reaction_positiv
@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Das ist gut zu hören.
@goto .endDialogue

# reaction_rejecting
@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Sind Sie sich sicher? Ein Kind kann viel Zeit beanspruchen. Aber wenn Sie dies schon bedacht haben, wird es wohl passen.
@goto .endDialogue

# reaction_sorry1
@char FemaleCharacter.Neutral
@char MaleCharacter.Surprised
{MaleChar}: Es tut mir leid, so habe ich die Frage nicht gemeint. Natürlich müssen Sie die Frage nicht beantworten.
@goto .endDialogue

# reaction_sorry2
@char FemaleCharacter.Neutral
@char MaleCharacter.Surprised
{MaleChar}: Es tut mir leid, ich habe diese Frage gestellt, ohne nachzudenken. 
{MaleChar}: Sie haben natürlich Recht. Ich werde auf meine nächsten Formulierungen besser achten.
@goto .endDialogue

# reaction_explaining
@char FemaleCharacter.Neutral
@char MaleCharacter.Talking
{MaleChar}: Tatsächlich ja. Wir hatten auch schon den Fall, dass ein Mann während des Gründungsprozesses aussteigen musste, 
{MaleChar}: weil er Vater wurde und die Einnahmen aus dem Unternehmen nicht für die Familie gereicht hätten.
@goto .endDialogue

;-------------------------------End of Dialogue
# endDialogue
@char MaleCharacter.Talking
{MaleChar}: Ich habe bereits ein paar Förderungen herausgesucht, die zu Ihrer Gründungsidee passen würden.
{MaleChar}: Ich kann Ihnen die Kontaktdaten geben. Wenn Sie wollen, können wir einen weiteren Termin für ein zweites Gespräch vereinbaren.
@hidePrinter
@showUI Map
@bgm BGM_Map volume:0.5 loop:true
@stop

# endKlickdummy
@printer KlickdummyText
Der Klickdummy ist nun beendet und kann geschlossen werden. Vielen Dank für das Testen.